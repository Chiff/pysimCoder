kill(all);

/* Coordinate indipendenti e derivate
   phi_w: angolo ruota rispetto alla verticale: positivo senso antiorario
   phi_b: angolo palla rispetto alla verticale: positivo senso antiorario
*/

var:[phi_b, phi_w, omega_b, omega_w];
depends(var,t);

inp:[Tw];
depends(inp,t);

dphi_w:diff(phi_w,t);
dphi_b:diff(phi_b,t);
ddphi_w:diff(phi_w,t,2);
ddphi_b:diff(phi_b,t,2);

/* Variabili ausiliarie */
/* Rotazione della palla phi_roll  */
R_2: R_w+R_b;
eq: phi_b*R_w=R_w*phi_w + R_b*phi_roll;
sol:solve(eq, phi_roll);
phi_roll: rhs(sol[1]);
dphi_roll: diff(phi_roll, t);

/* Energie */
T_b: 1/2*M_b*R_2^2*dphi_b^2+1/2*J_b*dphi_roll^2;
T_w: 1/2*J_w*dphi_w^2;
T: T_b+T_w;

U:M_b*g*R_2*cos(phi_b);

L:T-U;
D:1/2*d_w*dphi_w^2;
D:0;

/* Equazioni  di Lagrange */
eqn1:diff(diff(L,dphi_b),t)-diff(L,phi_b)=0;
eqn2:diff(diff(L,dphi_w),t)-diff(L,phi_w)=Tw;

eqn1:expand(eqn1);
eqn2:expand(eqn2);
eqn1:trigreduce(eqn1);
eqn2:trigreduce(eqn2);
eqn1:trigsimp(eqn1);
eqn2:trigsimp(eqn2);

/* Equazioni differenziali */
sol:linsolve([eqn1,eqn2],[ddphi_b,ddphi_w]);
s1:rhs(sol[1]);
s2:rhs(sol[2]);

s1:subst([diff(phi_w,t)=omega_w,diff(phi_b,t)=omega_b],s1);
s2:subst([diff(phi_w,t)=omega_w,diff(phi_b,t)=omega_b],s2);
s1:ratsimp(s1);
s2:ratsimp(s2);

/* State variables are phi_b, phi_w, omega_b, omega_w */
syseq:[omega_b,
       omega_w,
       s1,
       s2];

A:jacobian(syseq,var);
B:jacobian(syseq,inp);

A:at(A,[phi_w=0,omega_w=0,phi_b=0,omega_b=0,Tw=0]);
A:ratsimp(A);
B:at(B,[phi_w=0,omega_w=0,phi_b=0,omega_b=0,Tw=0]);
B:ratsimp(B);

A:at(A, [M_b = 0.106712, R_b = 0.105/2, J_b = 2.3608e-04, J_w = 48.218e-3, d_w = 0, R_w = 0.285, g = 9.81]);
B:at(B, [M_b = 0.106712, R_b = 0.105/2, J_b = 2.3608e-04, J_w = 48.218e-3, d_w = 0, R_w = 0.285, g = 9.81]);

N :48.0/18.0;
kt:-1.62e-4;
Kt:N*kt;

B:Kt*B;


